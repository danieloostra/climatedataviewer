! feature_lon_lat_time_depth.jnl
! Use file attributes to find which variables correspond to
! longitude, latitude, time in a Trajectory file
! The attributes will be:
!
! _CoordinateAxisType = "Lat"
! _CoordinateAxisType = "Lon"
! _CoordinateAxisType = "Time"
! 

LET all_varnames = ..varnames
LET nvars = ..nvars

REPEAT/I=1:`nvars` (DEFINE SYMBOL ivar = `all_varnames`;\
 LET all_attnames = ($ivar).attnames;\
 LET vartype = "nostring";\
 IF `IS_ELEMENT_OF_STR(all_attnames, "_CoordinateAxisType") GT 0` THEN LET vartype = "`($ivar)._CoordinateAxisType`";\
 IF `STRCMP(vartype,"Lon") EQ 0` THEN DEFINE SYMBOL data_x_var = ($ivar);\
 IF `STRCMP(vartype,"Lat") EQ 0` THEN DEFINE SYMBOL data_y_var = ($ivar);\
 IF `STRCMP(vartype,"Time") EQ 0` THEN DEFINE SYMBOL data_t_var = ($ivar);\
 IF `STRCMP(vartype,"Height") EQ 0` THEN DEFINE SYMBOL data_z_var = ($ivar);\
 IF `STRCMP(vartype,"Depth") EQ 0` THEN DEFINE SYMBOL data_z_var = ($ivar);\
 IF `($data_x_var"0|*>1")+($data_y_var"0|*>1")+($data_t_var"0|*>1")+($data_z_var"0|*>1") EQ 4` THEN EXIT/LOOP;\
 )

! Are longitude and latitude on the E axis of the dsg file? (profile data)

DEFINE SYMBOL vshape = `($data_x_var),RETURN=shape`
IF `($vshape"0|E>1|*>0)` THEN
   DEFINE SYMBOL ($data_x_var)_name = ($data_x_var)
   SET VAR/NAME=($data_x_var)_M ($data_x_var)
   LET/units=`($data_x_var)_M,return=units`/TITLE=`($data_x_var)_M,RETURN=title` \
    ($($data_x_var)_name) = EXPND_BY_LEN(($data_x_var)_M, ($rowvar),`($rowvar)[m=@sum]`)
ENDIF

DEFINE SYMBOL vshape = `($data_y_var),RETURN=shape`
IF `($vshape"0|E>1|*>0)` THEN
   DEFINE SYMBOL ($data_y_var)_name = ($data_y_var)
   SET VAR/NAME=($data_y_var)_M ($data_y_var)
   LET/UNITS=`($data_y_var)_M,RETURN=units`/TITLE=`($data_y_var)_M,RETURN=title` \
    ($($data_y_var)_name) = EXPND_BY_LEN(($data_y_var)_M, ($rowvar),`($rowvar)[m=@sum]`)
ENDIF

IF `($data_x_var%0|*>1%) + ($data_x_var%0|*>1%) LT 2` THEN
   MESSAGE/ERROR **ERROR Dataset must contain a Lon variable and a Lat variable
   EXIT/PROGRAM
ENDIF


IF `($its_trajectory_map"0|*>1") OR ($its_profile_map"0|*>1")` THEN
   IF ($rowvar"0|*>1") THEN DEFINE SYMBOL rowvar = rowsize

   DEFINE SYMBOL vshape = `($data_x_var),RETURN=shape`
   IF `($vshape"0|E>1|*>0) AND  (STRCMP("($data_x_var)", "($feature_variable)") NE 0)` THEN

! Expand feature variable into obs axis
      LET/TITLE="($ferret_title_0)" data_x_var_expand = EXPND_BY_LEN(($data_x_var), ($rowvar),`($rowvar)[m=@sum]`)
      DEFINE SYMBOL data_x_var = data_x_var_expand

   ENDIF

   DEFINE SYMBOL vshape = `($data_y_var),RETURN=shape`
   IF `($vshape"0|E>1|*>0) AND  (STRCMP("($data_y_var)", "($feature_variable)") NE 0)` THEN

! Expand feature variable into obs axis
      LET/TITLE="($ferret_title_0)" data_y_var_expand = EXPND_BY_LEN(($data_y_var), ($rowvar),`($rowvar)[m=@sum]`)
      DEFINE SYMBOL data_y_var = data_y_var_expand

   ENDIF

   IF ($data_z_var"0|*>1") THEN
   DEFINE SYMBOL vshape = `($data_y_var),RETURN=shape`
   IF `($vshape"0|E>1|*>0) AND  (STRCMP("($data_y_var)", "($feature_variable)") NE 0)` THEN

! Expand feature variable into obs axis
      LET/TITLE="($ferret_title_0)" data_y_var_expand = EXPND_BY_LEN(($data_y_var), ($rowvar),`($rowvar)[m=@sum]`)
      DEFINE SYMBOL data_y_var = data_y_var_expand

   ENDIF  ! IF ($data_z_var)
   ENDIF

ENDIF

!---------------- end of feature_lon_lat_time_depth.jnl -------------------------

