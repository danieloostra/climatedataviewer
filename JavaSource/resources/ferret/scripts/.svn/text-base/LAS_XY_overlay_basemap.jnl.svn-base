!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! $Header$ LAS_XY_overlay_basemap.jnl
!
! Overlays a basemap based on the data in ($basemap_url) and ($basemap_var).
!
! By default an appropriate etopo dataset is used to plot gray continents.

! Set defaults for any 'basemap_~' symbols that are not yet defined
SET DATA/SAVE

DEFINE SYMBOL basemap_area = `( ($region_x_range) * ($region_y_range) )/ (360 * 180)`


IF `($basemap_area) LT 0.2` THEN
  DEFINE SYMBOL basemap_url = ($basemap_url"etopo20")

  IF `($basemap_area) LT 0.08` THEN \
  DEFINE SYMBOL basemap_url = ($basemap_url"etopo5")

  IF `($basemap_area) LT 0.02` THEN
     IF `TEST_OPENDAP("http://ferret.pmel.noaa.gov/thredds/dodsC/data/PMEL/etopo2.nc") EQ 0` THEN \
        DEFINE SYMBOL basemap_url = "http://ferret.pmel.noaa.gov/thredds/dodsC/data/PMEL/etopo2.nc"
  ENDIF  

  IF `($basemap_area) LT 0.001` THEN
   IF `TEST_OPENDAP("http://ferret.pmel.noaa.gov/thredds/dodsC/data/PMEL/etopo1.nc") EQ 0` THEN \
      DEFINE SYMBOL basemap_url = "http://ferret.pmel.noaa.gov/thredds/dodsC/data/PMEL/etopo1.nc"
  ENDIF

ELSE 
  DEFINE SYMBOL basemap_url = ($basemap_url"etopo60")
ENDIF

DEFINE SYMBOL basemap_fill_type = FILL

DEFINE SYMBOL basemap_var = ($basemap_var"rose")
DEFINE SYMBOL basemap_palette = ($basemap_palette"gray")
DEFINE SYMBOL basemap_fill_type = ($basemap_fill_type"shade|fill|*>shade")
!DEFINE SYMBOL basemap_levels = ($basemap_levels"(0,10000,10000)")

! TODO:  Is the region setting for Z and T sufficient?

DEFINE SYMBOL basemap_region_z = ($basemap_region_z"k=1")
DEFINE SYMBOL basemap_region_t = ($basemap_region_t"l=1")

SET DATA ($basemap_url)

DEFINE SYMBOL basemap_data_num = `($basemap_var),return=dsetnum`

! Plot the data

DEFINE SYMBOL map_region_x = ($region_x)
IF `($REGION_X_RANGE) EQ 360` THEN
   DEFINE SYMBOL map_region_x = X=`($region_x_lo)-1`:`($region_x_hi)+1`
ENDIF

LET basevar = ($basemap_var)[d=($basemap_data_num),($map_region_x),($region_y),($basemap_region_z),($basemap_region_t)]

LET topo_dx = `xbox[GX=($basemap_var),I=1]`
LET topo_dy = `ybox[GY=($basemap_var),J=1]`

IF `(($region_x_range) LT topo_dx) OR  (($region_y_range) LT topo_dx)` THEN
   DEFINE SYMBOL topo_regrid = 1
   IF `($region_x_range) EQ 0` THEN  
      DEFINE AXIS/UNITS=degrees_east/MODULO/x=`($region_x_lo)-topo_dx`:`($region_x_lo)+topo_dx`/NPOINTS=5 x_super_fine
   ELSE
      DEFINE AXIS/UNITS=degrees_east/MODULO/($region_x)/NPOINTS=5 x_super_fine
   ENDIF
   IF `($region_y_range) EQ 0` THEN  
      DEFINE AXIS/UNITS=degrees_north/MODULO/y=`($region_y_lo)-topo_dx`:`($region_y_lo)+topo_dx`/NPOINTS=5 y_super_fine
   ELSE
      DEFINE AXIS/UNITS=degrees_north/($region_y)/NPOINTS=5 y_super_fine
   ENDIF
   DEFINE GRID/X=x_super_fine/y=y_super_fine g_super_fine

   LET basevar = ($basemap_var)[d=($basemap_data_num),GXY=g_super_fine,($basemap_region_z),($basemap_region_t)]
!   DEFINE SYMBOL basemap_xformat = PPL XFOR (dms)
!   DEFINE SYMBOL basemap_yformat = PPL YFOR (dms)
ENDIF

IF ($basemap_fill_type"FILL|FILL>1|fill>1|*>0") THEN
   SHADE/NOLABELS/NOKEY/PALETTE=($basemap_palette)/LEVELS=($basemap_levels)/SET basevar
   ($basemap_xformat)
   ($basemap_yformat)
   ($basemap_axisformatting)
   PPL SHADE
!!!!!   FILL/OVER/NOLABELS/NOKEY/PALETTE=($basemap_palette)/LEVELS=($basemap_levels) basevar

ELSE
  ($basemap_fill_type)/NOLABELS/NOKEY/PALETTE=($basemap_palette)/LEVELS=($basemap_levels)/SET basevar
   ($basemap_xformat)
   ($basemap_yformat)
   ($basemap_axisformatting)
   PPL SHADE

ENDIF

! Clean up
   say BASEMAP LandResolution IS ($basemap_url)  Region area is ($basemap_area)

CANCEL DATA ($basemap_url)

SET DATA/RESTORE
!-------------------------- end of LAS_XY_overlay_basemap-------------------
