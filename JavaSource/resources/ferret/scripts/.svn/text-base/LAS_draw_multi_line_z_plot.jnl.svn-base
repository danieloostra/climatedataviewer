! LAS_draw_multi_line_z_plot.jnl
!  Do a multi-line 1-D plot in z. 
! 7/2010    ACM updates for V7.2 multi-variable plots

set mode ver:always

! By default use graticules only the independent axis. 
! Dependent-axis ones can be a mess. The user can turn them on 
! if desired.  If explicitly turned off, do not draw them.


!SET MODE LINECOLORS:8
!PPL COLOR,7,70,70,70  ! gray
!PPL COLOR,8,100,100,100  ! white
!DEFINE SYMBOL graticuleColor = 7

!CANCEL MODE GRATICULE
!IF ($ferret_use_graticules"0|none>0|default>0|*>1) THEN
!  SET MODE GRATICULE:(DASH,COLOR=($graticuleColor))
!ELSE
!   DEFINE SYMBOL qualifiers_0 = /HGRATICULE=DASH,COLOR=($graticuleColor))
!   IF `($ferret_use_graticules"1|0|default>1|1|*>1) EQ 0` THEN CANCEL SYMBOL qualifiers_0
!ENDIF

CANCEL MODE GRATICULE
DEFINE SYMBOL qualifiers_0 = /HGRATICULE=DASH,COLOR=($graticuleColor))
IF `($ferret_use_graticules"1|none>0|*>1) EQ 0` THEN CANCEL SYMBOL qualifiers_0

GO LAS_pp_set_region_labels  ! for first dataset/variable

! Initialize datasets beyond the first one. Keep
! titles for the plot legend and units for labeling 
! the plot axes.

IF `($data_count"0") GT 6` THEN  DEFINE SYMBOL data_count = 6

DEFINE SYMBOL units_0 = `UPCASE("($data_0_units)")`
DEFINE SYMBOL all_units_same = 1

DEFINE SYMBOL unique_units_0 = ($data_0_units)
DEFINE SYMBOL nunique = 0

IF `($data_count"0") GT 6` THEN 
   DEFINE SYMBOL data_count = 6
   DEFINE SYMBOL reset_count = 1
ENDIF

REPEAT/range=1:`($data_count)-1`/NAME=qdat (  \
  DEFINE SYMBOL qdat = `qdat`; \
  GO LAS_initialize_region ($qdat) ;  \
  GO LAS_initialize_data  ($qdat) ; \
  GO LAS_check_var_direction; \
  IF ($error_status"0|*>1") THEN EXIT/LOOP; \
  DEFINE SYMBOL plot_arg = ($plot_arg),($ferret_plot_var); \
  DEFINE SYMBOL ferret_plot_var_($qdat) = ($ferret_plot_var); \ 
  DEFINE SYMBOL ferret_title_($qdat) = ($ferret_plot_title); \ 
  LET tlen = STRLEN("($ferret_title_($qdat))"); \
  LET title_len = `MAX(tlen, title_len)`; \
  DEFINE SYMBOL units_($qdat) = `UPCASE("($data_($qdat)_units)")`; \
  DEFINE SYMBOL all_units_same = `($all_units_same) AND (STRCMP("($units_0)", "($units_($qdat))") EQ 0)`; \
  GO LAS_pp_set_region_labels \
)

! If default graticules setting, and units are all the same, put
! graticules on both axes.
IF `($all_units_same"0|0|*>1") AND ($qualifiers_0"0|*>1")` THEN 
   CANCEL SYMBOL qualifiers_0
   SET MODE GRATICULE:(DASH,COLOR=($graticuleColor))
ENDIF

! No title; variable names and units are listed in the legend.
DEFINE SYMBOL qualifiers = ($qualifiers)/TITLE=" "

! If there are two variables with different units, scale each independently, putting
! one dependent axis on the left an the other on the right.

IF `(($data_count"0") EQ 2) AND (STRCMP("($units_0)", "($units_1)") NE 0)` THEN 

! Plot second variable in red, with its axis on the right.

   DEFINE VIEWPORT/x=0:1/y=0:1 right_axis_view
   IF ($ferret_annotations"0|*>1") THEN DEFINE VIEWPORT/axes/x=.1:.9/y=.13:.94 right_axis_view
   SET VIEW right_axis_view
   SET VAR/UNITS="@P2@AC($data_1_units)" ($ferret_plot_var_1)
   PLOT($qualifiers)/AXES=1,0,0,0/NOKEY/COLOR=red/SET ($ferret_plot_var_1)
      GO remove_logo
      IF `($labnum_x%0|*>1%)` THEN GO unlabel ($labnum_x)
      IF `($labnum_y%0|*>1%)` THEN GO unlabel ($labnum_y)
      IF `($labnum_z%0|*>1%)` THEN GO unlabel ($labnum_z)
      IF `($labnum_t%0|*>1%)` THEN GO unlabel ($labnum_t)
      IF `($labnum_year%0|*>1%)` THEN GO unlabel ($labnum_year)
      IF `($labnum_dset%0|*>1%)` THEN GO unlabel ($labnum_dset)
      IF `($labnum_datitl%0|*>1%)` THEN GO unlabel ($labnum_datitl)
      IF `($labnum_dods%0|*>1%)` THEN GO unlabel ($labnum_dods)
      IF `($labnum_calend%0|*>1%)` THEN GO unlabel ($labnum_calend)
   PPL PLOT
   DEFINE SYMBOL extra_y_space = 0.5 ! start labels higher up to give space for a
			             ! a formatted axis on top.

! Set up for plotting 1st variable, in black, with its axis on the left.

   DEFINE SYMBOL qualifiers = ($qualifiers)/AXES=0,1,1,1
   DEFINE SYMBOL data_count = 1
   DEFINE SYMBOL plot_arg = ($ferret_plot_var_0)
   DEFINE SYMBOL two_axes = 1
   SET VAR/UNITS="@AC($data_0_units)" ($ferret_plot_var_0)

   SET VIEW full
   IF ($ferret_annotations"0|*>1") THEN 
      DEFINE VIEWPORT/axes/x=.1:.9/y=.13:.94 full_axes1
      SET VIEW full_axes1
   ENDIF
ENDIF

! If line style is just symbols, we need to do lines separately to get different colors.

! If 2 variables, label units on axis; otherwise dont
IF `($data_count) GT 2 AND ($two_axes"0") EQ 0` THEN DEFINE SYMBOL qualifiers = ($qualifiers)/NOLAB 

DEFINE SYMBOL color0 = black
DEFINE SYMBOL color1 = red
DEFINE SYMBOL color2 = green
DEFINE SYMBOL color3 = blue
DEFINE SYMBOL color4 = lightblue
DEFINE SYMBOL color5 = purple

IF `(($all_units_same) EQ 0) AND (($data_count) GT 2)` THEN
   SET WIN/ASP=1.3
   LET margin_del = 0.08
   PPL AXLSZE,,0.08
   PPL AXNSIG,,2

   DEFINE SYMBOL iplot = 0
   DEFINE SYMBOL plot_vars_0 = ($ferret_plot_var_0)
   DEFINE SYMBOL group_label_0 = ($data_0_units)
   REPEAT/RANGE=1:($data_count)/name=m (\
      DEFINE SYMBOL m = `m`; \
      DEFINE SYMBOL mm = `m-1`; \
      DEFINE SYMBOL in_units = ($units_($mm)); \
      GO collect_units_vars )

! Draw the other plot axes scaled for each set of variables with common units
! Plot axis with first set of units will be on the left.
! Draw the plots for other sets of variables as /SYM/COLOR=8 (white) so that
! only the axis shows. The variables will be plotted individually below
! so that we can control the line color.

   LET xpl = .98
   LET ypl = `1 - margin_del* (($iplot)-1)`
   IF ($ferret_annotations"0|*>1") THEN LET ypl = `.94 - margin_del* (($iplot)-1)`

   REPEAT/RANGE=2:`($iplot)+1`/NAME=m (\
      DEFINE SYMBOL vm = `m-1`; \
      DEFINE VIEWPORT/X=0:`xpl`/y=0:`ypl` v($vm); \
      IF ($ferret_annotations"0|*>1") THEN DEFINE VIEWPORT/axes/x=.1:`xpl`/y=.13:`ypl` v($vm); \
      SET VIEW v($vm); \
      PLOT/AXES=1,0,0,0/SYM=dot/COLOR=8/NOLAB ($plot_vars_($vm));\
      LABEL/NOUSER `($ppl$xlen)/2`, `($ppl$ylen) + 0.5`, 0, 0, 0.12, @AC($group_label_($vm)); \
      LET ypl = `ypl + margin_del` \
   )
   DEFINE SYMBOL y_top_loc = ($ppl$ylen)

! Draw the lines each in its own viewport

   LET ypl = `1 - margin_del* (($iplot)-1)`
   IF ($ferret_annotations"0|*>1") THEN LET ypl = `.94 - margin_del* (($iplot)-1)`

! Draw the first variable
   DEFINE SYMBOL vm = 0
   DEFINE VIEWPORT/X=0:`xpl`/y=0:`ypl` v0_($vm)
   IF ($ferret_annotations"0|*>1") THEN DEFINE VIEWPORT/axes/x=.1:`xpl`/y=.13:`ypl` v0_($vm)
   SET VIEW v0_0
   PLOT($qualifiers)($qualifiers_0)/AXES=0,1,1,1/COLOR=($color($vm))/NOLAB ($ferret_plot_var_($vm))
   LABEL/NOUSER `($ppl$xlen)/2`, `-0.5`, 0, 0, 0.12, @AC($group_label_($vm))

   REPEAT/RANGE=2:($data_count)/name=m (\
      DEFINE SYMBOL vm = `m-1`; \
      DEFINE VIEWPORT/X=0:`xpl`/y=0:`ypl` v0_($vm);\
      IF ($ferret_annotations"0|*>1") THEN DEFINE VIEWPORT/axes/x=.1:`xpl`/y=.13:`ypl`  v0_($vm);\
      SET VIEW v0_($vm);\
      PLOT($qualifiers)/AXES=0,0,0,0/COLOR=($color($vm))/NOLAB ($ferret_plot_var_($vm)) \
   )

   ! space for labels above the top axis.
   DEFINE SYMBOL extra_y_space = `($Y_TOP_LOC) - ($ppl$ylen) - 0.2` 
ELSE

SET VIEW FULL

IF ($ferret_annotations"0|*>1") THEN 
   DEFINE VIEWPORT/axes/x=.1:.9/y=.13:.94 full_axes2
   SET VIEW full_axes2
ENDIF

IF `($ferret_line_or_sym%0|sym>1|*>0%)` THEN  
   PLOT($qualifiers_0)/NOKEY/SET/COLOR=8 ($plot_arg)
      IF `($labnum_x%0|*>1%)` THEN GO unlabel ($labnum_x)
      IF `($labnum_x%0|*>1%)` THEN GO unlabel ($labnum_y)
      IF `($labnum_z%0|*>1%)` THEN GO unlabel ($labnum_z)
      IF `($labnum_t%0|*>1%)` THEN GO unlabel ($labnum_t)
      IF `($labnum_year%0|*>1%)` THEN GO unlabel ($labnum_year)
      IF `($labnum_dset%0|*>1%)` THEN GO unlabel ($labnum_dset)
      IF `($labnum_datitl%0|*>1%)` THEN GO unlabel ($labnum_datitl)
      IF `($labnum_dods%0|*>1%)` THEN GO unlabel ($labnum_dods)
      IF `($labnum_calend%0|*>1%)` THEN GO unlabel ($labnum_calend)
      IF `($title_size%0|*>1%)` THEN PPL LABSET ($title_size)
      IF `($ferret_dep_axis_scale"0|*>1")` THEN PPL XAXIS ($ferret_dep_axis_scale)
      PPL ylab
      PPL xlab ($data_0_units)
   PPL PLOT

   PLOT($qualifiers)/NOKEY/NOLAB/OVER/COLOR=black ($ferret_plot_var_0)
   IF `($data_count"0") GT 1` THEN PLOT($qualifiers)/NOKEY/OVER/COLOR=red ($ferret_plot_var_1)
   IF `($data_count"0") GT 2` THEN PLOT($qualifiers)/NOKEY/OVER/COLOR=green ($ferret_plot_var_2)
   IF `($data_count"0") GT 3` THEN PLOT($qualifiers)/NOKEY/OVER/COLOR=blue ($ferret_plot_var_3)
   IF `($data_count"0") GT 4` THEN PLOT($qualifiers)/NOKEY/OVER/COLOR=lightblue ($ferret_plot_var_4)
   IF `($data_count"0") GT 5` THEN PLOT($qualifiers)/NOKEY/OVER/COLOR=purple ($ferret_plot_var_5)
ELSE

   PLOT($qualifiers)($qualifiers_0)/NOKEY/SET ($plot_arg)
      IF `($labnum_x%0|*>1%)` THEN GO unlabel ($labnum_x)
      IF `($labnum_y%0|*>1%)` THEN GO unlabel ($labnum_y)
      IF `($labnum_z%0|*>1%)` THEN GO unlabel ($labnum_z)
      IF `($labnum_t%0|*>1%)` THEN GO unlabel ($labnum_t)
      IF `($labnum_year%0|*>1%)` THEN GO unlabel ($labnum_year)
      IF `($labnum_dset%0|*>1%)` THEN GO unlabel ($labnum_dset)
      IF `($labnum_datitl%0|*>1%)` THEN GO unlabel ($labnum_datitl)
      IF `($labnum_dods%0|*>1%)` THEN GO unlabel ($labnum_dods)
      IF `($labnum_calend%0|*>1%)` THEN GO unlabel ($labnum_calend)
      IF `($title_size%0|*>1%)` THEN PPL LABSET ($title_size)
      IF `($ferret_dep_axis_scale"0|*>1")` THEN PPL YAXIS ($ferret_dep_axis_scale)
      PPL ylab
      PPL xlab ($data_0_units)
   PPL PLOT
ENDIF
ENDIF ! same units for all 

IF ($labnum_calend%0|*>1%) THEN
   DEFINE SYMBOL n_left_labels = `($n_left_labels"0") + 1`
   DEFINE SYMBOL upper_left_($n_left_labels) = ($lab($labnum_calend))
   DEFINE SYMBOL calendar_lab = ($upper_left_($n_left_labels"0"))
ENDIF

DEFINE SYMBOL ax_vert_save = ($ax_vert"D")
DEFINE SYMBOL ax_horiz_save = ($ax_horiz"D")

DEFINE SYMBOL yaxis_min_save = ($yaxis_min)
DEFINE SYMBOL yaxis_max_save = ($yaxis_max)

! Draw the legend.

! Make an invisible xy plot. The legline script doesnt work well with z and t plots.
IF `($two_axes"0") EQ 1` THEN DEFINE SYMBOL data_count = 2

DEFINE VIEWPORT/x=0:1/y=0:1 overview
show view
SET VIEW overview
PLOT/NOLAB/NOAX/I=1:2/VLIM=100:102 i/0

! The start (x,y) position for each line drawn on the key
LET x1 = -0.2
IF `($data_count) GT 3` THEN LET x1 = -0.5
IF `(($data_count) GT 3) AND (title_len GT 40)` THEN LET x1 = -0.6
LET xkey = {`x1` , `x1` , `x1` , `0.6*($ppl$xlen)`, `0.6*($ppl$xlen)`, `0.6*($ppl$xlen)`,     0, 0}
LET ykey = {-0.65, -0.85, -1.05,            -0.65,             -0.85,             -1.05, -1.25, -1.25}

LET titlesiz = 0.11
IF `title_len GE 40 AND ($data_count"0") GT 3` THEN LET titlesiz = 0.10
IF `title_len GE 50 AND ($data_count"0") GT 3` THEN LET titlesiz = 0.09
IF `title_len GE 60 AND ($data_count"0") GT 3` THEN LET titlesiz = 0.08

REPEAT/range=1:`($data_count)`/NAME=m (; \
   DEFINE SYMBOL m = `m`;  \
   DEFINE SYMBOL lnum = `m-1` ; \
   LET xpos = xkey[i=($m)]; LET ypos = ykey[i=($m)]; \
   GO LAS_legend_line \
   `xpos+x1` 0.3 `ypos` `m + ($nlinecolors)*($thicknum)` `titlesiz` "($ferret_title_($lnum))" ($legend_qual);\   
)

! IF ($reset_count"0|*>1") THEN 
!   LABEL/nouser `xkey[i=@ave]` `ykey[i=@min]-0.1` 0 0  `titlesiz` "Plot maximum of 6 lines"


! The results script needs the actual axis directions. Restore them.

DEFINE SYMBOL ax_vert = ($ax_vert_save)
IF ($ax_vert"|D>1|*>0") THEN CANCEL SYMBOL ax_vert
DEFINE SYMBOL ax_horiz = ($ax_horiz_save)
IF ($ax_horiz"|D>1|*>0") THEN CANCEL SYMBOL ax_horiz

DEFINE SYMBOL yaxis_min = ($yaxis_min_save)
DEFINE SYMBOL yaxis_max = ($yaxis_max_save)

! --------------- End of LAS_draw_multi_line_z_plot  ---------------------------

