!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! $Header$
! Trajectory_label_cruise_pts.jnl
!
! Add to annotations the upper right with number of cruises
! and number of valid and missing points shown.

! If all labeling turned off just exit.
IF `($ferret_label"1|0|1|*>1") EQ 0` THEN EXIT/SCRIPT

LET check_var = $1%($ferret_plot_var)%

! Fill with a known value to distinguish between input missing values
! and values missing because of the different length of cruises.

! Define variable cruise by cruise-length
! Count number of good and missing values in each cruise

LET datmax = check_var[I=@max]
IF `(MISSING(datmax,100) EQ 100)  AND (MISSING(datmax,10) EQ 10)` THEN LET datmax = 0  

IF `datmax LE 1` THEN
   LET fillval = 100
ELSE
   LET fillval =  `10^(INT(LOG(datmax))+1)`
ENDIF

LET var_fill = MISSING(check_var,`fillval`)
LET var_fill_z =  EXPNDI_BY_Z_COUNTS(var_fill, xsequence(rowsize),`longest`)

LET var_flag_z = IF var_fill_z EQ `fillval` THEN 1
LET nbad_in_cruise =  var_flag_z[K=@NGD]
LET ngood_in_cruise = var_fill_z[K=@NGD] - var_flag_z[K=@NGD]

LET trajectory_mask = 1
IF ($text_mask_trajectory"0|*>1") THEN 
   LET trajectory_mask = XSEQUENCE(($text_mask_trajectory))
ELSE
   IF `IS_ELEMENT_OF_STR_N(varnames, "($trajectory_variable)") NE 0` THEN 
      LET trajectory_m = 1+0*_m[ge=($trajectory_variable)]
      LET trajectory_mask =  XSEQUENCE(trajectory_m)
   ENDIF
ENDIF

LET ngood_chosen_cruises = subset_mask* trajectory_mask* ngood_in_cruise
LET nbad_chosen_cruises = subset_mask* trajectory_mask*  nbad_in_cruise

! Just compute one of these, and subtract from the total in dataset

IF `($ngood_data"0|*>1") EQ 0` THEN 
   IF `($ferret_memory"25.6") LT 700` THEN SET MEM/SIZ=700
   LET allsize = `check_var,RETURN=isize`
   DEFINE SYMBOL nbad_data = `nbad_chosen_cruises[i=@SUM]`
   DEFINE SYMBOL ngood_data = `allsize - ($nbad_data)`
ENDIF

LET allvars = ..varnames

IF ($cruises_shown"1|*>0") THEN DEFINE SYMBOL cruises_shown = `trajectory_mask[i=@sum]`
DEFINE SYMBOL cruises_shown = ($cruises_shown"($cruise_max)")

!DEFINE SYMBOL database_table_label = `cruise_id.database_table`

! c_s used to add plural s to a label if more than one cruise.
CANCEL SYMBOL c_s
IF `($cruises_shown"0") GT 1` THEN DEFINE SYMBOL c_s = s

DEFINE SYMBOL note_num = `($note_num"0") + 1`
DEFINE SYMBOL note_($note_num)_lab =  ($cruises_shown) cruise($c_s), ($ngood_data) valid obs
IF ($nbad_data"0|*>1") THEN DEFINE SYMBOL note_($note_num)_lab = \
 ($cruises_shown) cruise($c_s), ($ngood_data) valid obs (($nbad_data) missing)

! If the constraint was where the color variable is valid, then no need for (0 missing)

IF `($has_missing"0|0>1|*>0") AND ($nbad_data"1|0>1|*>0")` THEN \
 DEFINE SYMBOL note_($note_num)_lab = ($cruises_shown) cruise($c_s), ($ngood_data) valid obs

! Notation about the decimation
IF ($piecewise_decimation"0|*>1") THEN 
   DEFINE SYMBOL note_num = `($note_num"0") + 1`
   LET fraction = ($nout_lonlat)/ABS(($ngood_data)-($nbad_data"0"))
   DEFINE SYMBOL note_($note_num)_lab = Decimated with Piecewise Linear Method to ($nout_lonlat) \
good points,  fraction `fraction,PREC=4`
ENDIF

!---------------- end of Trajectory_label_cruise_pts.jnl -------------------------
