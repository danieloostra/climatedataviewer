!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! LAS_pp_colorby_traj.jnl
! Draw the correlation plot with icons colored by trajectory ID.

! Create the 'traj' variables and symbols
! Create a new title that includes subsampling info

! Restart setting plot qualifiers for correlation plot. 
DEFINE SYMBOL qualifiers = /NOLAB

IF `($data_1_var"0|*>1") EQ 0` THEN
  MESSAGE/ERROR **ERROR Correlation product requires two variables to be selected
  EXIT/PROGRAM
ENDIF

DEFINE SYMBOL traj_max = `($rowvar),RETURN=size`

! The mask is 1 for start of trajectories 

let tmask = if ID_DATA[i=@ddb] eq 1 then 1 else 0
let traj_mask1 = tmask[gx=($ferret_plot_var_0)@ASN]
let traj_mask2 = if i[gx=($ferret_plot_var_0)] eq 1 then 1 else 0
let traj_mask = if (traj_mask1+traj_mask2) eq 1 then 1

LET pvar = constraints_mask* ($ferret_plot_var_0)
LET qvar = constraints_mask* ($ferret_plot_var_1)
LET dvar = constraints_mask* ID_DATA

LET data_1_var_c =  EXPNDI_BY_Z(pvar, traj_mask,`ntrajs`,`longest`)
LET data_var_c =  EXPNDI_BY_Z(qvar, traj_mask,`ntrajs`,`longest`)

! Set up to plot the data and the linear fit
LET pmask =  subset_mask * data_1_var_c
LET qmask =  subset_mask * data_var_c

LET p = XSEQUENCE( subset_mask * data_1_var_c)
LET q = XSEQUENCE( subset_mask * data_var_c)

! Set up to make polygon plot. If there are not too many trajs
! color the data according to traj.


DEFINE SYMBOL qualifiers = ($qualifiers)/OVER/NOAXES/NOKEY/NOLAB/

IF `($traj_max) LE ($traj_max_labels)` THEN
   DEFINE SYMBOL ferret_poly_palette = ten_by_levels
   DEFINE SYMBOL qualifiers = ($qualifiers)/PALETTE=($ferret_poly_palette)

! If undefined, set polygon scale according to number of points
   IF ($ferret_poly_scale"1|*>0") THEN 
      GO LAS_set_poly_scale
      DEFINE SYMBOL ferret_poly_scale 0.5*($ferret_poly_scale)
   ENDIF
   IF `($ferret_poly_scale) GE 0.4` THEN DEFINE SYMBOL qualifiers = ($qualifiers)/LINE/THICK=1
ELSE
   DEFINE SYMBOL qualifiers = ($qualifiers)/PALETTE=black
   DEFINE SYMBOL poly_scale = 0.4
ENDIF

DEFINE SYMBOL qualifiers = ($qualifiers)/LINE
DEFINE SYMBOL poly_command POLY($qualifiers)
DEFINE SYMBOL line_color = blue

! If there is just one traj, draw it with PLOT/VS which is faster than
! drawing polygons. Symbol 20 is a triangle.

sh sym traj_max, traj_max_labels

IF `($traj_max) EQ 1` THEN

   IF ($connect_dots"0|0|1|*>1") THEN \
   PLOT/NOAX/VS/OVER/LINE=6/NOLAB pmask, qmask
   PLOT/NOAX/VS/OVER/SYM=20/NOLAB/thick=1 pmask, qmask

   DEFINE SYMBOL ngood_data = `pmask[i=@ngd]`

ELIF `($traj_max) LE ($traj_max_labels)` THEN

   LET p = XSEQUENCE( subset_mask * data_1_var_c)  ! for regression line
   LET q = XSEQUENCE( subset_mask * data_var_c)


   ! Use different color for each traj 1-10, 11-21, ...
   ! Use different symbols for each set of 10

   LET ncolors = 10
   LET nshapes = 20
   LET polygon_set = `INT(0.5+($traj_max)/nshapes)`
   IF `polygon_set*nshapes LT ($traj_max)`  THEN LET polygon_set = `polygon_set+1`

! See polyshape script for the list of available pre-defined shapes.

! Set the shapes with the same script as in the script 200symbols.jnl
   GO LAS_pp_set_traj_shapes

   LET cmax = `MIN(($traj_max),ncolors)`
   LET c1 = 1
   DEFINE SYMBOL poly_command ($poly_command)/LEVELS=(`c1`,`cmax+1`,1)
   LET ishape = 1
   LET icolor = 1

   LET colr_index = MOD(i[i=1:`ntrajs`],`ncolors`)
   LET allcolors = IF colr_index EQ 0 THEN `ncolors` ELSE colr_index 
   LET dmask = subset_mask * allcolors + 0*qmask

   LET transpmask = TRANSPOSE_XZ(pmask)
   LET transqmask = TRANSPOSE_XZ(qmask)
   LET transdmask = TRANSPOSE_XZ(dmask)

   LET c1 = 1
   LET cn = `ncolors`

   REPEAT/RANGE=1:`ntrajs`:`ncolors`/NAME=ic (\
    DEFINE SYMBOL ishape = `ishape`;\
    LET zx = transpmask[k=`c1`:`cn`];\
    LET zy = transqmask[k=`c1`:`cn`];\
    LET zd = transdmask[k=`c1`:`cn`];\
    LET xpolydat = XSEQUENCE(zx);\
    LET ypolydat = XSEQUENCE(zy);\
    LET dpolydat = XSEQUENCE(zd);\
    LET all_mask = IF xpolydat+ypolydat+dpolydat THEN 1;\
    LET all_x = COMPRESSI(all_mask*xpolydat);\
    LET all_y = COMPRESSI(all_mask*ypolydat);\
    LET all_d = COMPRESSI(all_mask*dpolydat);\
    LET do_it = `xpolydat[i=@ngd] GT 0`;\
     ;\
     IF `do_it AND ($connect_dots"0|0|1|*>1")` THEN \
     PLOT/NOAX/VS/OVER/LINE=6/NOLAB xpolydat, ypolydat ;\
     ;\
    IF `do_it` THEN GO polymark ($poly_command) all_x, all_y, all_d, ($shape($ishape)), ($poly_scale) ;\
     ;\
     LET c1 = `c1 + ncolors`;\
     LET cn = `cn + ncolors`;\
     IF `cn GT ntrajs` THEN LET cn = `ntrajs`;\
     LET ishape = `ishape+1`;\
     PPL shaset protect;\
    )
! Note need to protect the colors from one set of 10 to the next
  
ELSE ! all trajs

   GO polymark ($poly_command) p q p \
     ($ferret_poly_shape"triangle|delta|square|diamond|pentagon|hexagon|circle|star|plus|ex|*>triangle") \
     ($poly_scale)

   DEFINE SYMBOL ngood_data = `p[i=@ngd]`
ENDIF

! End of file ------------ LAS_pp_colorby_traj.jnl--------------------------
